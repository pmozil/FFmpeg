/*
 * This file is part of FFmpeg.
 *
 * FFmpeg is free software; you can redistribute it and/or
 * modify it under the terms of the GNU Lesser General Public
 * License as published by the Free Software Foundation; either
 * version 2.1 of the License, or (at your option) any later version.
 *
 * FFmpeg is distributed in the hope that it will be useful,
 * but WITHOUT ANY WARRANTY; without even the implied warranty of
 * MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the GNU
 * Lesser General Public License for more details.
 *
 * You should have received a copy of the GNU Lesser General Public
 * License along with FFmpeg; if not, write to the Free Software
 * Foundation, Inc., 51 Franklin Street, Fifth Floor, Boston, MA 02110-1301 USA
 */

layout(push_constant, std430) uniform pushConstants {
    ivec2 plane_sizes[3];
    int plane_offs[3];
    int plane_strides[3];
    int dw[3];
    int wavelet_depth;
};

void dequant(int outIdx, int idx, int qf, int qs) {
    int32_t val = inBuffer[idx];
    val = sign(val) * ((abs(val) * qf + qs) >> 2);
    outBuf0[outIdx] = outBuf1[outIdx] = val;
}

int32_t dequant_imm(int32_t val, int qf, int qs) {
    return sign(val) * ((abs(val) * qf + qs) >> 2);
}

void proc_slice(int slice_idx) {
    const int plane = int(gl_GlobalInvocationID.y);

    const int base_idx = slice_idx * DWT_LEVELS * 8;
    const int base_slice_idx = slice_idx * DWT_LEVELS * 3 + plane * DWT_LEVELS;


    Slice s = slices[base_slice_idx];
    int offs = s.offs;

    for(int level = 0; level < wavelet_depth; level++) {
        s = slices[base_slice_idx + level];

        for(int orient = int(bool(level)); orient < 4; orient++) {
            int32_t qf = quantMatrix[base_idx + level * 8 + orient];
            int32_t qs = quantMatrix[base_idx + level * 8 + orient + 4];

            const int subband_idx = plane * DWT_LEVELS * 4
                                        + 4 * level + orient;

            const SubbandOffset sub_off = subband_offs[subband_idx];
            int img_idx = plane_offs[plane] + sub_off.base_off
                            + s.top * sub_off.stride + s.left;
            LUTState lut = dirac_golomb_lut[inBuffer[offs]];

            int x = 0;
            int32_t val = 0;
            for(int y = 0; y < s.tot_v; y++) {
                int32_t buf[8];
                int cur_idx = 0;
                while (x < s.tot_h) {
                    val  = (val << lut.val0_bits) | lut.val0;
                    buf[0] = dequant_imm((val - 1) * lut.sign, qf, qs);
                    buf[1] = dequant_imm(lut.val1, qf, qs);
                    buf[2] = dequant_imm(lut.val2, qf, qs);
                    buf[3] = dequant_imm(lut.val3, qf, qs);
                    buf[4] = dequant_imm(lut.val4, qf, qs);
                    buf[5] = 0;
                    buf[6] = 0;
                    buf[7] = 0;
                    val = lut.num == 0 ? val : lut.val;
                    for (cur_idx = 0; x + cur_idx < s.tot_h && cur_idx < lut.num; cur_idx++) {
                        outBuf0[img_idx + x + cur_idx] = buf[cur_idx];
                        outBuf1[img_idx + x + cur_idx] = buf[cur_idx];
                    }

                    x += lut.num;

                    lut = dirac_golomb_lut[lut.state + inBuffer[offs]];
                    offs++;
                }
                img_idx += sub_off.stride;
                if (cur_idx != lut.num - 1) {
                    x = cur_idx;
                    for (;x < lut.num; x++) {
                        outBuf0[img_idx + x] = buf[x];
                        outBuf1[img_idx + x] = buf[x];
                    }
                } else {
                    x = 0;
                }
            }
        }
    }
}

void main() {
    int idx = int(gl_GlobalInvocationID.x);
    proc_slice(idx);
}
